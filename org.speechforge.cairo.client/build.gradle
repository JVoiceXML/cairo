apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
    maven { 
        url "https://oss.sonatype.org/content/repositories/snapshots" 
    }
}

dependencies {
    compile group: 'org.jvoicexml', name: 'org.mrcp4j', version: '0.3-SNAPSHOT'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-jul', version: '2.11.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: '2.11.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.11.2'
    compile group: 'javax.sip', name: 'jain-sip-api', version: '1.2.1.4'
    compile group: 'javax.sip', name: 'jain-sip-ri', version: '1.3.0-91'
    compile group: 'javax.sdp', name: 'nist-sdp', version: '1.0'
    compile group: 'javax.media', name: 'jmf', version: '2.1.1e'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    compile project(':org.speechforge.cairo.sip')
    compile project(':org.speechforge.cairo.rtp')
    
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

}

jar {
    manifest {
        attributes('Implementation-Title': 'The module that provides client side mrcpv2 capabilities for speech recognition applications',
                   'Implementation-Vendor': 'switch',
                   'Implementation-Version': version,
                   'Built-By'       : System.properties['user.name'],
                   'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                   'Created-By'     : "Gradle ${gradle.gradleVersion}",
                   'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                   'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}")
    }
    baseName 'org.speechforge.cairo.client'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { 
                MavenDeployment deployment -> signing.signPom(deployment) 
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: JVOICEXML_OSSRH_USERNAME, password: JVOICEXML_OSSRH_PASSWORD)
            }
    
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: JVOICEXML_OSSRH_USERNAME, password: JVOICEXML_OSSRH_PASSWORD)
            }
    
            pom.project {
                name 'org.speechforge.cairo.client'
                groupId = JVOICEXML_GROUP
                packaging 'jar'
                description 'The module that provides client side mrcpv2 capabilities for speech recognition applications'
                url 'https://github.com/JVoiceXML/cairo'
                
                scm {
                    connection 'git@github.com:JVoiceXML/cairo.git'
                    developerConnection 'git@github.com:JVoiceXML/cairo.git'
                    url "https://github.com/JVoiceXML/JVoiceXML/tree/release/${version}"
                }
                    
                licenses {
                    license {
                        name 'GNU Lesser General Public License, Version 2.1'
                        url 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }
    
                developers {
                    developer {
                        id 'schnelle'
                        name 'Dirk Schnelle-Walka'
                        email 'dirk.schnelle@jvoicexml.org'
                    }
                }
            }
        }
    }
}
